import funkin.graphics.adobeanimate.FlxAtlasSprite;
import flixel.FlxG;
import funkin.audio.FunkinSound;
import funkin.play.character.MultiSparrowCharacter;
import funkin.play.GameOverSubState;
import funkin.play.PlayState;
import funkin.Preferences;
import funkin.util.Constants;
import funkin.util.HapticUtil;
import funkin.play.character.BaseCharacter;
import funkin.data.character.CharacterDataParser;

using StringTools;

class HexGlitcherBoyfriendCharacter extends MultiSparrowCharacter
{
  public var wireBF:BaseCharacter;

  function new() {
    super('bf-hex-glitcher');
  }

  function onCreate(event:ScriptEvent)
  {
    super.onCreate(event);

    if (!this.debug)
    {
      wireBF = CharacterDataParser.fetchCharacter('bf-hex-wire');
      if (wireBF == null) return; // should NOT happen but-
      wireBF.zIndex = 150;
      wireBF.flipX = false;
      wireBF.antialiasing = true;

      PlayState.instance.currentStage?.add(wireBF);
      PlayState.instance.currentStage?.scriptGet("wireGroup")?.add(this.wireBF);
      PlayState.instance.currentStage?.refresh(); // Apply z-index.
      wireBF.setPosition(this.x, this.y);
    }
  }

  function onAnimationFrame(name:String, frameNumber:Int, frameIndex:Int)
  {
    super.onAnimationFrame(name, frameNumber, frameIndex);

    if (!HapticUtil.hapticsAvailable) return;

    // BF's mic drops.
    if (name == 'firstDeath' && frameNumber == 27)
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }

    // BF's balls pulsating.
    if (name == 'deathLoop' && (frameNumber == 0 || frameNumber == 18))
    {
      HapticUtil.vibrate(0, 0.1, 0.1, 1);
    }
  }

  override function playAnimation(name:String, restart:Bool, ignoreOther:Bool)
  {
    if (name == "fakeoutDeath" && !this.debug)
    {
      doFakeoutDeath();
    }
    else
    {
      super.playAnimation(name, restart, ignoreOther);
    }

    if (wireBF != null)
    {
      wireBF.playAnimation(name, restart, ignoreOther);
      wireBF.setPosition(this.x, this.y);
    }
  }

  function doFakeoutDeath():Void
  {
    FunkinSound.playOnce(Paths.sound("gameplay/gameover/fakeout_death"), 1.0);

    var bfFakeout:FlxAtlasSprite = new FlxAtlasSprite(this.x - 440, this.y - 240, Paths.animateAtlas("characters/bfFakeOut", "shared"));
    GameOverSubState.instance.add(bfFakeout);
    bfFakeout.zIndex = 1000;
    bfFakeout.playAnimation('');
    // We don't want people to miss this.
    GameOverSubState.instance.mustNotExit = true;
    bfFakeout.onAnimationComplete.add(() -> {
      bfFakeout.visible = false;
      this.visible = true;
      GameOverSubState.instance.mustNotExit = false;
      this.playAnimation('firstDeath', true, true);
      // Play the "blue balled" sound. May play a variant if one has been assigned.
      GameOverSubState.playBlueBalledSFX();
    });
    bfFakeout.visible = true;
    this.visible = false;
  }
}
